include(CMakeFindDependencyMacro)
find_dependency(Qt6 @Qt6_VERSION_MAJOR@.@Qt6_VERSION_MINOR@)

file(GLOB __@PROJ_NAME_UC@_AVAILABLE_COMPONENT_TARGETS
    RELATIVE "${CMAKE_CURRENT_LIST_DIR}"
    "${CMAKE_CURRENT_LIST_DIR}/*/@CMAKE_PROJECT_NAME@*Targets.cmake"
)

message(STATUS "[@CMAKE_PROJECT_NAME@] Available Components:")
foreach(component_target ${__@PROJ_NAME_UC@_AVAILABLE_COMPONENT_TARGETS})
    message(STATUS "- ${component_target}")
endforeach()

# Components were requested
if(@CMAKE_PROJECT_NAME@_FIND_COMPONENTS)
    message(DEBUG "[@CMAKE_PROJECT_NAME@] Components were requested.")

    # Check for and add each request
    foreach(component ${@CMAKE_PROJECT_NAME@_FIND_COMPONENTS})
        set(component_target ${component}/@CMAKE_PROJECT_NAME@${component}Targets.cmake)

        if (";${__@PROJ_NAME_UC@_AVAILABLE_COMPONENT_TARGETS};" MATCHES ";${component_target};")
            message(DEBUG "[@CMAKE_PROJECT_NAME@] Inlcuding matched component: ${component}.")
            include("${CMAKE_CURRENT_LIST_DIR}/${component_target}")
        elseif(@CMAKE_PROJECT_NAME@_FIND_REQUIRED_${component}})
            set(@CMAKE_PROJECT_NAME@_FOUND False)
            set(@CMAKE_PROJECT_NAME@_NOT_FOUND_MESSAGE "Unsupported component: ${component}")
        endif()
    endforeach()
else() # Components were not requested
    message(DEBUG "[@CMAKE_PROJECT_NAME@] No components were requested.")

    # Add all components
    foreach(component_target ${__@PROJ_NAME_UC@_AVAILABLE_COMPONENT_TARGETS})
        message(DEBUG "[@CMAKE_PROJECT_NAME@] Inlcuding available component target: ${component_target}.")
        include("${CMAKE_CURRENT_LIST_DIR}/${component_target}")
    endforeach()
endif()
