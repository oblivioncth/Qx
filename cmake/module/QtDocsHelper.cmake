function(configure_qt_doc_link qt_prefix)
    if(NOT DEFINED QT_HELP_GEN_PATH)
        if(CMAKE_SYSTEM_NAME STREQUAL Windows)
            set(DEFAULT_HELP_GEN_PATH "${qt_prefix}/bin/qhelpgenerator.exe")
        elseif(CMAKE_SYSTEM_NAME STREQUAL Linux)
            set(DEFAULT_HELP_GEN_PATH "${qt_prefix}/libexec/qhelpgenerator")
        endif()

        if(EXISTS "${DEFAULT_HELP_GEN_PATH}")
            set(QT_HELP_GEN_PATH "${DEFAULT_HELP_GEN_PATH}" PARENT_SCOPE)
            message(STATUS "Automatically configured qhelpgenerator path")
        else()
            message(WARNING "Could not determine qhelpgenerator path automatically. Please set QT_HELP_GEN_PATH to its location manually")
        endif()
    else()
        message(STATUS "Path of qhelpgenerator set externally to: ${QT_HELP_GEN_PATH}")
    endif()

    if(NOT DEFINED QT_DOCS_DIR)
        if(EXISTS "${qt_prefix}/doc/qtcore/qtcore.index")
            set(QT_DOCS_DIR "${qt_prefix}/doc" PARENT_SCOPE)
            message(STATUS "Automatically configured Qt documentation path.")
        else()
            message(WARNING "Could not determine Qt documentation path automatically. Please set QT_DOCS_DIR to their location manually")
        endif()
    else()
        message(STATUS "Path of Qt documentation set externally to: ${QT_DOCS_DIR}")
    endif()
endfunction()
